/* ▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄▄
 * ▓ Creator: Yalla Nkardaz (Ялла Нкардаз де Тудерий) aka Demer Nkardaz
 * ▓ Title:   DSL KeyPad (Diacritics-Spaces-Letters KeyPad)
 * ▓ Version: 0.1.2
 * ▓ Description: Multilingual input tool for typing languages based on
 *                Latin & Cyrillic scripts, special characters, and historical
 *                scripts (Old Turkic, Permic, Hungarian, Italic, Runic,
 *                Phoenician, Glagolitic, etc.).
 * ▓ Repository: https://github.com/DemerNkardaz/DSL-KeyPad
 * ▓ License:    MIT
 * ▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓▓
*/

#SingleInstance Force
SetKeyDelay(0, 50)
A_HotkeyInterval := 1000
A_MaxHotkeysPerInterval := 50

initialized := False

#Include <External\JSON>
#Include <External\function_clip_send>
#Include <External\function_gui_button_icon>

#Include <utils>
#Include <hotstrings>
#Include <default_variables>
; Supplementary include with binds that calls functions/methods
#Include Data\binds.ahk
; Supplementary include with character entries generated by Loops
#Include Data\characters.ahk
#Include <Classes\class_mods_injector>
; Automatically created/updated by mods injector class
; Dynamically loads mods pasted into \Mods\ directory
#Include *i Mods\injector_pre_init.ahk

#Include <Classes\class_util>
#Include <Classes\class_variable_parser>
#Include <Classes\class_progress_bar>
#Include <Classes\class_progress_tooltip>
#Include <Classes\class_character_block>
#Include <Classes\class_unicode_web_resource>
#Include <Classes\class_search>

#Include <Classes\class_application>
#Include <Classes\class_language>
#Include <Classes\class_update>
#Include <Classes\class_config>
#Include <Classes\class_mods_gui>
#Include <Classes\class_fonts>
#Include <Classes\class_character_entity>
#Include <Classes\class_character_registrar>
#Include <Classes\class_character_library>
#Include <Classes\class_character_crafter>
#Include <Classes\class_character_inserter>
#Include <Classes\class_script_processor>
#Include <Classes\class_favorites>

#Include <Classes\class_user_defined_recipes>
#Include <Classes\class_character_recipe_handler>
#Include <Classes\class_auxiliary>
#Include <Classes\class_character_legend>
#Include <Classes\class_ui_panel>
#Include <Classes\class_ui_panel_filter>
#Include <Classes\class_key_event>
#Include <Classes\class_bind_list>
#Include <Classes\class_layout>
#Include <Classes\class_text_handlers>
#Include <Classes\class_long_press>
#Include <Classes\class_hotstrings_latex>
#Include <Classes\class_tempature_converter>
#Include <Classes\class_dev>

ChrLib.CountOfUpdate()
App.SetTrayItems()
Panel.SetPanelData()
GlyphsPanel.SetPanelData()

initialized := True

; Automatically created/updated by mods injector class
; Dynamically loads mods pasted into \Mods\ directory
#Include *i Mods\injector_post_init.ahk

; Legacy code, saved for future rework
GREPizeSelection(GetCollaborative := False) {
	CustomAfterStartEmdash := (Cfg.Get("Paragraph_After_Start_Emdash", "CustomRules", "") != "") ? Cfg.Get("Paragraph_After_Start_Emdash", "CustomRules", "") : "ensp"
	CustomDialogue := (Cfg.Get("GREP_Dialog_Attribution", "CustomRules", "") != "") ? Cfg.Get("GREP_Dialog_Attribution", "CustomRules", "") : "no_break_space"
	CustomThisEmdash := (Cfg.Get("GREP_ThisEmdash", "CustomRules", "") != "") ? Cfg.Get("GREP_ThisEmdash", "CustomRules", "") : "no_break_space"
	CustomInitials := (Cfg.Get("GREP_Initials", "CustomRules", "") != "") ? Cfg.Get("GREP_Initials", "CustomRules", "") : "thinspace"

	Punctuations := "[" ChrLib.Get("reversed_question", "inverted_exclamation", "inverted_question", "double_exclamation", "double_exclamation_question", "double_question", "double_question_exclamation", "interrobang", "interrobang_inverted") ".,!?…”’»›“]"

	GREPRules := Map(
		"start_emdash", {
			grep: "^" ChrLib.Get("emdash") "\s",
			replace: ChrLib.Get("emdash", CustomAfterStartEmdash)
		},
		"dialogue_emdash", {
			grep: "(?<=" Punctuations ")\s" ChrLib.Get("emdash") "\s",
			replace: ChrLib.Get(CustomDialogue "[1,3]", "emdash")
		},
		"this_emdash", {
			grep: "(?<!" Punctuations ")\s" ChrLib.Get("emdash") "\s",
			replace: ChrLib.Get(CustomThisEmdash, "emdash", "space")
		},
		"nums", {
			grep: "(?<=\d)\s(?=\d{3})",
			replace: ChrLib.Get("no_break_space")
		},
		"paragraph_end", {
			grep: "(?<=[а-яА-ЯёЁa-zA-Z])\s(?=[а-яА-ЯёЁa-zA-Z]{1,12}[" Punctuations "]*$)",
			replace: ChrLib.Get("no_break_space")
		},
		"initials", {
			grep: "([A-ZА-ЯЁ]\.)\s([A-ZА-ЯЁ]\.)\s([A-ZА-ЯЁ][a-zа-яё]+)",
			replace: "$1" . ChrLib.Get(CustomInitials) . "$2" . ChrLib.Get(CustomInitials) . "$3"
		},
		"initials_reversed", {
			grep: "([A-ZА-ЯЁ][a-zа-яё]+)\s([A-ZА-Яё]\.)\s([A-ZА-ЯЁ]\.)",
			replace: "$1" . ChrLib.Get(CustomInitials) . "$2" . ChrLib.Get(CustomInitials) . "$3"
		},
		"single_letter", {
			grep: "(?<![а-яА-ЯёЁa-zA-Z])([а-яА-ЯёЁa-zA-Z])\s",
			replace: "$1" ChrLib.Get("no_break_space")
		},
		"russian_conjunctions", {
			grep: "\s(бы|ли|то|же)(?![а-яА-Я])",
			replace: ChrLib.Get("no_break_space") "$1"
		},
		"russian_conjunctions_2", {
			grep: "\s(из|до|для|на|но|не|ни|то|по|со|Из|До|Для|На|Но|Не|Ни|То|По|Со)\s",
			replace: ChrLib.Get("space") "$1" ChrLib.Get("no_break_space")
		},
	)

	BackupClipboard := ClipboardAll()
	if !GetCollaborative {
		PromptValue := ""
		A_Clipboard := ""

		Send("{Shift Down}{Delete}{Shift Up}")
		ClipWait(0.50, 1)
		PromptValue := A_Clipboard
		A_Clipboard := ""
	} else {
		PromptValue := ParagraphizeSelection(True)
		Sleep 100
	}

	if (PromptValue != "") {
		TotalLines := 0
		SplittedLines := StrSplit(PromptValue, "`r`n")
		ModifiedValue := ""

		for index in SplittedLines {
			TotalLines++
		}

		CurrentLine := 0
		for _, rule in GREPRules {
			for i, line in SplittedLines {
				SplittedLines[i] := RegExReplace(line, rule.grep, rule.replace)
			}
		}

		for line in SplittedLines {
			CurrentLine++
			EndLine := CurrentLine < TotalLines ? "`r`n" : ""
			ModifiedValue .= line . EndLine
		}

		A_Clipboard := ModifiedValue
		ClipWait(0.5, 1)
		Send("{Shift Down}{Insert}{Shift Up}")
	}

	Sleep 500
	A_Clipboard := BackupClipboard
}

ParagraphizeSelection(SendCollaborative := False) {
	BackupClipboard := A_Clipboard
	PromptValue := ""
	A_Clipboard := ""

	Send("^c")
	ClipWait(0.50, 1)
	PromptValue := A_Clipboard
	A_Clipboard := ""

	if (PromptValue != "") {
		CustomParagraphBeginning := (Cfg.Get("Paragraph_Beginning", "CustomRules", "") != "") ? Cfg.Get("Paragraph_Beginning", "CustomRules", "") : "emsp"
		CustomAfterStartEmdash := (Cfg.Get("Paragraph_After_Start_Emdash", "CustomRules", "") != "") ? Cfg.Get("Paragraph_After_Start_Emdash", "CustomRules", "") : "ensp"

		blockRules := [CustomParagraphBeginning, CustomAfterStartEmdash]

		for i, blockRule in blockRules {
			if blockRule = "noad" {
				blockRules[i] := ""
			}
		}

		CustomParagraphBeginning := blockRules[1]
		CustomAfterStartEmdash := blockRules[2]

		TotalLines := 0
		ModifiedValue := ""
		SplittedLines := StrSplit(PromptValue, "`r`n")

		for index in SplittedLines {
			TotalLines++
		}

		CurrentLine := 0
		for line in SplittedLines {
			CurrentLine++
			EndLine := CurrentLine < TotalLines ? "`r`n" : ""
			LocalModify := RegExReplace(
				line, "^" . ChrLib.Get("emdash") . "\s+",
				ChrLib.Get("emdash") . ChrLib.Get(CustomAfterStartEmdash)
			)
			ModifiedValue .= ChrLib.Get(CustomParagraphBeginning) . LocalModify . EndLine

		}

		if !SendCollaborative {
			A_Clipboard := ModifiedValue
			ClipWait(0.250, 1)
			Sleep 1000
			Send("^v")
		} else {
			A_Clipboard := BackupClipboard
			return ModifiedValue
		}

		;SendText(ModifiedValue)
	}

	Sleep 1000
	A_Clipboard := BackupClipboard
}


ShowInfoMessage("tray_app_started")

ShowEntryPreview() {
	IB := InputBox("", "", "w256 h92")
	if IB.Result = "Cancel"
		return
	else {
		ChrLib.EntryPreview(IB.Value)
	}
}

<^>^Home:: ShowEntryPreview()

#SuspendExempt
>^F10:: KeyboardBinder.MonitorToggler(KeyboardBinder.disabledByUser = !False ? True : False, "User", "Monitor")
#SuspendExempt False

<^>+Esc:: ExitApp